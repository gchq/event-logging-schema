import java.util.regex.Pattern

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
    //must be applied to all projects including root
    id "ca.cutterslade.analyze" version "1.9.1" apply true
}

def projectGroup = "event-logging-transformer"


//if the project has a value for the passed property (i.e from the cmd line via -PpropName=xxx)
//use that, else use a default value
ext.getPropertyOrDefault = { propName, defaultValue -> 
    def val;
    if (project.hasProperty(propName) && project.getProperty(propName) != "unspecified" && project.getProperty(propName) != "") {
        val = project.getProperty(propName)
        println "Using propery [${propName}] with value [${val}]"
    } else {
        val = defaultValue
        println "Propery [${propName}] has no value, using default value [${val}]"
    }
    return val;
}

ext.versions = [
    //------event-logging--------------
    eventLogging: getPropertyOrDefault('version', 'SNAPSHOT'),

    //------------3rd-party------------
    assertj: '3.24.2',
    javaDiffUtils: '4.0',
    jackson: '2.8.9',
    junit_jupiter: '5.10.0',
    logback: '1.2.3',
    saxon: '9.6.0-6',
    slf4j: '1.7.36',
    //systemRules: '1.19.0',
    systemLambda: '1.2.1',
    zzDUMMYzz: 'makes sorting easier'
]

allprojects {
    version versions.eventLogging
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'ca.cutterslade.analyze'
    apply plugin: 'idea'

    group "$projectGroup"

    repositories {
        mavenCentral()
        mavenLocal()
    }

    configurations {
        all {
            exclude group: "junit", module: "junit"
            exclude group: "org.slf4j", module: "slf4j-log4j12"
            exclude group: "log4j", module: "log4j"
            exclude group: "commons-logging", module: "commons-logging"

            resolutionStrategy {
                dependencySubstitution {
                    substitute module('log4j:log4j') using module('org.slf4j:log4j-over-slf4j:$versions.slf4j')
                }

                forcedModules = [
                ]
            }
        }
    }
}

